AC_INIT(zero-install.c)
AM_INIT_AUTOMAKE(zero-install, 0.1.24)
AC_PROG_MAKE_SET

LAZYFS_VERSION=0d1d24
AC_SUBST(LAZYFS_VERSION)

AC_PROG_CC

if test -z "$PKG_CONFIG"; then
  PKG_CONFIG=pkg-config
fi
AC_SUBST(PKG_CONFIG)

AC_MSG_CHECKING(that pkg-config runs)
if "$PKG_CONFIG" --version > /dev/null 2>&1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  AC_MSG_ERROR([*** $PKG_CONFIG missing ***

\'$PKG_CONFIG --version\' failed to run - make sure it is inside one of the
directories in your PATH environment variable!

pkg-config comes with the developement packages for GTK+-2.0, available at:

    http://www.gtk.org])
fi

AC_DEFUN(PKG_REQUIRE, [
AC_MSG_CHECKING(that $1 (version >= $2) is installed)
if "$PKG_CONFIG" --atleast-version=$2 $1 ; then
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
  if "$PKG_CONFIG" --exists $1 ; then
    AC_MSG_ERROR(Current version is too old)
  else
    AC_MSG_ERROR(Package is not installed)
  fi
fi
])

dnl When doing 'make distcheck', use $prefix/bin for /bin, etc.
AC_ARG_WITH(distcheck,,
	[ROOT_PREFIX='$(prefix)'],
	[ROOT_PREFIX=''])
AC_SUBST(ROOT_PREFIX)

dnl Specify the location of init.d.
AC_MSG_CHECKING(for init.d directory)
AC_ARG_WITH(initdir,
	[  --with-initdir=dir    give the (init.d) directory for the 0install script]
	[                        [/etc/init.d]],
	INITDIR="$withval",
	INITDIR="/etc/init.d")
AC_MSG_RESULT($INITDIR)
AC_SUBST(INITDIR)
[
if [ x"$ROOT_PREFIX" = x ]; then
  if [ ! -d "$INITDIR" ]; then
    echo
    echo "Directory $INITDIR not found."
    echo "Use '--with-initdir=/etc/init.d' or similar to set the location for"
    echo "the 0install script, which can be used to start and stop the "
    echo "zero-install helper."
    exit 1
  fi
fi
]

AC_MSG_CHECKING(for user to run helper as)
AC_ARG_WITH(user,
  [  --with-user=user       run helper application as 'user'],
  [HELPER_USER="$withval"],
  [HELPER_USER="yes"])
if [[ "$HELPER_USER" = "yes" ]]; then
  AC_MSG_RESULT(missing)
  echo
  echo "Must specify a user to run the helper as, eg:"
  echo "./configure --with-user=zeroinst"
  echo "(having created a new user called 'zeroinst' for the purpose)"
  echo "Using 'root' is not recommended, for security reasons."
  exit 1
fi
AC_MSG_RESULT($HELPER_USER)
AC_SUBST(HELPER_USER)

[
if [ x"$ROOT_PREFIX" = x ]; then
  if ! id "$HELPER_USER" > /dev/null; then
    echo "User '$HELPER_USER' does exist!"
    exit 1
  fi
fi
]

dnl PKG_REQUIRE(libxml-2.0, 2.0.0)
echo '  (D-BUS is available from freedesktop.org)'
PKG_REQUIRE(dbus-1, 0.20)

AC_CHECK_HEADER(expat.h, [], [
  echo 'You need to install the expat headers (libexpat-dev)' >&2
  exit 1
])

AC_OUTPUT(Makefile)
